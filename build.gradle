/*
   Project JaCaMo web

   Gradle build file for JaCaMo Applications
   October 15, 2019
*/

plugins {
  id "java-library"
  id "eclipse"
  id "maven-publish"
  id "com.github.johnrengelman.shadow" version "5.2.0"
  id "com.github.node-gradle.node" version "2.2.0"
}

defaultTasks 'run'

group   'org.jacamo'
version '0.7-SNAPSHOT'

repositories {
	mavenCentral()
	maven { url "https://raw.githubusercontent.com/jacamo-lang/mvn-repo/master" }
	maven { url "https://repo.gradle.org/gradle/libs-releases" }
	flatDir {
		dirs 'lib'
	}
}

dependencies {
	api 'org.jacamo:jacamo-rest:0.7.1-SNAPSHOT'
	
	//org.apache.curator.framework
	implementation 'org.apache.zookeeper:zookeeper:3.5.4-beta'
	implementation 'org.apache.curator:curator-framework:4.0.1'
	
	//org.gradle:gradle-tooling-api
	implementation gradleApi()

	//websockets for simulations
	implementation 'javax.websocket:javax.websocket-api:1.1'
	implementation 'org.glassfish.tyrus:tyrus-server:1.1'
	implementation 'org.glassfish.tyrus:tyrus-container-grizzly:1.1'

	// https://mvnrepository.com/artifact/org.eclipse.jgit/org.eclipse.jgit
	implementation 'org.eclipse.jgit:org.eclipse.jgit:5.6.0.201912101111-r'

	// https://mvnrepository.com/artifact/net.openhft/compiler
	implementation 'net.openhft:compiler:2.3.1'
	testImplementation group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main {
		java {
			srcDir 'src/env'
		}
        resources {
            srcDir 'src/agt'
            srcDir 'src/org'
            srcDir 'src/resources'
        }
    }
}

/*
 *
 * JaCaMo projects for lauch configuration and tests
 *
 */

task run (type: JavaExec, dependsOn: 'classes') {
    group      ' JaCaMo'
    description 'runs the JaCaMo platform'
    doFirst {
        mkdir 'log'
    }
    main 'jacamo.infra.JaCaMoLauncher'
    args 'src/jcm/main.jcm'
    classpath sourceSets.main.runtimeClasspath
}

task fixTab {
    ant.fixcrlf(eol: 'lf',  includes: '**/*.txt,**/*.bat, **/*.adoc', srcdir: '.')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml', srcdir: 'src')
    ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.asl,**/*.jcm', srcdir: '.')
    //ant.fixcrlf(tab: 'remove', tablength: '4', javafiles: 'true', includes: '**/*.java,**/*.xml,**/*.asl,**/*.jcm,**/*.txt', srcdir: 'doc')
}

jar {
    baseName 'jacamo-web'
    manifest {
      attributes(
        'Class-Path': file(configurations.compileClasspath.find { it.name.startsWith("jason-") }).name,
        'Main-Class': 'jacamo.infra.JaCaMoLauncher',
        'args': 'src/jcm/main.jcm'
      )
    }
}

/*
 *
 * Heroku support
 *
 */

task copyToLib(type: Copy) {
    into "$buildDir/libs"
    from(configurations.compileClasspath)
}

task stage(dependsOn: ['shadowJar', 'copyToLib', 'clean']) {
	copyToLib.mustRunAfter clean
	jar.mustRunAfter copyToLib
}

stage.dependsOn(copyToLib)

/*
 *
 * Java Docs
 *
 */

// the following lines is used to avoid errors while generating javadoc
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    baseName 'jacamo-web'
    classifier = 'javadoc'
    from '${docsDir}/../../doc/api'
}

/*
 *
 * Maven publication
 *
 */

task sourceJar (type : Jar) {
    baseName 'jacamo-web'
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
            artifactId 'jacamo-web'
        }
    }
}

// transfer local maven artifact to our maven2 repository at github
task publishMavenGitHub(dependsOn: [ 'build', 'publishToMavenLocal']) {
	doLast {
	    def wdir = System.getProperty("user.home")+'/.m2/repository/org/jacamo/jacamo-web'
	    def rdir = System.getProperty("user.home")+'/pro/jacamo-mvn-repo'
	    exec {
	        commandLine 'git', 'pull'
	        workingDir rdir
	    }
	    copy {
	        from wdir
	        into rdir + '/org/jacamo/jacamo-web'
	    }
	    exec {
	        commandLine 'git', 'add', '*'
	        workingDir rdir
	    }
	    exec {
	        commandLine 'git', 'commit', '-a', '-m', 'new version of jacamo web '+project.version
	        workingDir rdir
	    }
	    exec {
	        commandLine 'git', 'push'
	        workingDir rdir
	    }
	}
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

/*
 *
 * NPM test javascripts
 *
 */

node {
    /* gradle-node-plugin configuration
       https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
       Task name pattern:
       ./gradlew npm_<command> Executes an NPM command.
    */
    // Version of node to use.
    version = '14.17.5'
    // Version of yarn to use.
    yarnVersion = '1.22.5'
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = true
}

String testsExecutedMarkerName = "${projectDir}/.tests.executed"
task testYarn(type: YarnTask, dependsOn: 'yarn') {
    dependsOn assemble
    // force Jest test runner to execute tests once and finish the process instead of starting watch mode
    environment CI: 'true'
    args = ['run', 'test']
    inputs.files fileTree('src/js')
    inputs.file 'yarn.lock'
    // allows easy triggering re-tests
    doLast {
        new File(testsExecutedMarkerName).text = 'delete this file to force re-execution JavaScript tests'
    }
    outputs.file testsExecutedMarkerName
}
check.dependsOn testYarn

task buildYarn(type: YarnTask, dependsOn: 'yarn') {
    dependsOn assemble
    // force Jest test runner to execute tests once and finish the process instead of starting watch mode
    environment CI: 'true'
    args = ['run', 'build']
    inputs.files fileTree('src/js')
    inputs.file 'yarn.lock'
}
build.dependsOn buildYarn


/*
 *
 * Compile javascript if bundle does not exists
 *
 */

if ( !file("src/resources/js/jacamoweb.js").exists() ) {
	allprojects {
    tasks.shadowJar.dependsOn tasks.build
		tasks.run.dependsOn tasks.build
	}
}

/*
 *
 * Clean compiled files
 *
 */

clean {
    delete 'bin'
    delete 'build'
    delete 'log'
    delete testsExecutedMarkerName /*Used on testYarn*/
}
